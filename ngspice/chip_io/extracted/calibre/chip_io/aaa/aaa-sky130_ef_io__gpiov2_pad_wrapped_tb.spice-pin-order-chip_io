*** This is a chip_io smoke test with transient supply ramps

* Most models come from here:
 .lib ~/foss/pdks/sky130A/libs.tech/ngspice/sky130.lib.spice tt

****.include /home/mk/foss/pdks/sky130_fd_pr/models/corners/tt/discrete.spice

* Include chip_io .subckt extracted using Calibre:
 .include ../netlists/chip_io.extracted.ngspice

* This device is also missing from the libraries:
 .include ./sky130_fd_io__condiode.spice
 .include ./sky130_fd_pr__model__parasitic__diode_ps2nw.spice 
 .include ./sky130_fd_pr__model__parasitic__diode_pw2dn.spice
 .include ./sky130_fd_pr__model__parasitic__diode_ps2dn.spice

***************************************

*********************
Xsky130_ef_io__gpiov2_pad_wrapped 
+ VSS		; VSSD  	   
+ VSS		; VSSA  	   
+ VSS		; VSSIO 	   
+ VSS		; VSSIO_Q	   
+ VDD3V3	; VDDIO 	   
+ VDD3V3	; VDDIO_Q	   
+ PAD 		; PAD		   
+ OPEN1		; PAD_A_ESD_0_H    
+ ZERO		; IB_MODE_SEL	   
+ ONE3V3	; ENABLE_INP_H     
+ ONE3V3	; ENABLE_H	   
+ VDD3V3	; VDDA  	   
+ ONE3V3	; ENABLE_VDDA_H    
+ VDD1V8	; VCCD  	   
+ ZERO		; OE_N  	   
+ ZERO		; VTRIP_SEL	   
+ VDD1V8	; VCCHIB	   
+ ZERO		; ENABLE_VSWITCH_H  
+ OUT		; OUT		   
+ ZERO		; HLD_OVR	   
+ ONE1V8	; DM[2] 	   
+ ZERO		; ANALOG_SEL	   
+ ONE3V3	; HLD_H_N	   
+ ZERO		; ANALOG_EN	   
+ ZERO		; INP_DIS	   
+ ZERO		; ANALOG_POL	   
+ ZERO		; DM[0] 	   
+ ONE1V8	; DM[1] 	   
+ ZERO		; SLOW  	   
+ IN		; IN		   
+ ONE1V8	; ENABLE_VDDIO     
+ OPEN2		; TIE_LO_ESD	   
+ VDD3V3	; VSWITCH	   
+ OPEN3		; AMUXBUS_A	   
+ OPEN4		; AMUXBUS_B	   
+ sky130_ef_io__gpiov2_pad_wrapped


vvss		VSS		0 		dc 	0
vvdd1v8		VDD1V8		0 		pwl	0 0 3u  1.8  1m 1.8
vvdd3v3		VDD3V3		0 		pwl	0 0 2u  3.3  1m 3.3

vzero		ZERO		0		dc	0
vone1v8		ONE1V8		0		pwl	0 0 5.5u 0 5.6u 1.8  1m 1.8
vone3v3		ONE3V3		0		pwl	0 0 5.5u 0 5.6u 3.3  1m 3.3

vout		OUT		0		pwl	0 0 8u 0  8.1u 1.8 11u 1.8 11.1u 0 15u 0
rload		PAD		0		1000K

.OPTION RSHUNT=1e15
**.OPTION SAVECURRENTS
.SAVE All(v)
.SAVE v(pad) v(out) i(vvdd3v3) i(vvdd1v8) v(vdd3v3) v(vdd1v8) v(ONE1V8) v(ONE3V3) i(vone1v8) i(vone3v3) i(vout) i(rload)
.TRAN 10n 15u


.END
